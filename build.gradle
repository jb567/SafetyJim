import com.moandjiezana.toml.Toml

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.moandjiezana.toml:toml4j:0.7.2'
    }
}

plugins {
    id 'nu.studer.jooq' version '2.0.11'
    //id 'com.sedmelluq.jdaction' version '1.0.2'
    id "com.github.johnrengelman.shadow" version "2.0.2"
    id 'com.github.ben-manes.versions' version '0.17.0'
}

group 'org.samoxive'
version '3.1.1'

apply plugin: 'java'
apply plugin: 'application'
mainClassName = "org.samoxive.safetyjim.Main"

sourceCompatibility = 1.8




repositories {
    mavenCentral()
    jcenter()
}



dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.8.0-beta1'
    jooqRuntime group: 'org.postgresql', name: 'postgresql', version: '42.2.2'
    compile group: 'org.postgresql', name: 'postgresql', version: '42.2.2'
    compile 'org.jooq:jooq'
    compile 'com.moandjiezana.toml:toml4j:0.7.2'
    compile 'net.dv8tion:JDA:3.5.1_343'
    compile 'com.zaxxer:HikariCP:2.7.8'
    compile "com.joestelmach:natty:0.13"
    compile 'org.ocpsoft.prettytime:prettytime:4.0.1.Final'
    compile 'com.squareup.okhttp3:okhttp:3.10.0'
    compile "io.vertx:vertx-web:3.5.1"
    compile 'com.auth0:java-jwt:3.3.0'
    compile 'com.google.code.gson:gson:2.8.2'
    compile 'log4j:log4j:1.2.17'
    compile 'org.reflections:reflections:0.9.11'
    compileOnly 'org.projectlombok:lombok:1.16.20'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.1.0'
    testRuntimeOnly    'org.junit.jupiter:junit-jupiter-engine:5.1.0'
    testCompile 'org.junit.jupiter:junit-jupiter-params:5.1.0'
    testCompile 'org.mockito:mockito-core:2.11.0'
    testCompile 'org.junit.platform:junit-platform-launcher:1.2.0'
}

jooq {
    def toml = new Toml().read(new File('./config.toml').text)
    def host      = toml.getString('database.host')
    def usernameT = toml.getString('database.user')
    def passwordT = toml.getString('database.pass')
    def port      = toml.getLong('database.port')
    def db        = toml.getString('database.name')

    version = '3.9.5'
    edition = 'OSS'
    sample(sourceSets.main) {
        jdbc {
            driver = 'org.postgresql.Driver'
            url = 'jdbc:postgresql://' + host + ':' + port + '/' + db
            user = usernameT
            password = passwordT
        }
        generator {
            name = 'org.jooq.util.DefaultGenerator'
            strategy {
                name = 'org.jooq.util.DefaultGeneratorStrategy'
            }
            target {
                packageName = 'org.samoxive.jooq.generated'
            }
            database {
                name = 'org.jooq.util.postgres.PostgresDatabase'
                inputSchema = 'public'
            }
        }
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'org.samoxive.safetyjim.Main'
    }
}

shadowJar {
    archiveName = "safetyjim.jar"
}

test {
    useJUnitPlatform()
}